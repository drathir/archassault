# Maintainer: ArchAssault <team@archassault.org>
pkgname=python2-requests_toolbox-git
pkgver=20141113.r171
pkgrel=1
pkgdesc="A collection of utilities for python-requests, but don't really belong in requests proper."
arch=('any')
url="https://github.com/sigmavirus24/requests-toolbelt"
license=('BSD')
depends=('python2')
makedepends=('git' 'python2-setuptools')
sha512sums=('SKIP')
source=("${pkgname}::git+https://github.com/sigmavirus24/requests-toolbelt.git")

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${pkgname}"
  python2 setup.py build
}

package() {
  cd "${pkgname}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -Dm644 README.rst "${pkgdir}"/usr/share/"${pkgname}"/README.rst
}
