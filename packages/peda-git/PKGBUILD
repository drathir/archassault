# Maintainer: ArchAssault <team archassault.org>
pkgname=peda-git
pkgver=r51.327db44
pkgrel=1
groups=('archassault' 'archassault-exploits')
pkgdesc="A Python Exploit Development Assistant for GDB"
arch=('any')
url='https://github.com/longld/peda'
license=('custom')
depends=('python2' 'gdb' 'nasm')
makedepends=('git')
source=("git+https://github.com/longld/peda.git"
	"peda.py.patch")
install='peda-git.install'
sha512sums=('SKIP'
            '8991761c04c05cec56c01553954f53037f9276a24f7d9e3dad5e0092f08cb683b96c727f7829ad51b3d85ab0a53e6e3a2b805af49517b113ccce838f65346f08')

pkgver() {
  cd "$srcdir/peda"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/peda" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  cd "$srcdir"/peda
  patch -Np1 -i "$srcdir"/peda.py.patch
}


package() {
  cd "$srcdir/peda"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/share/$pkgname/lib"
  install -Dm644 peda.py "$pkgdir/usr/share/$pkgname/"
  install -Dm644 lib/* "$pkgdir/usr/share/$pkgname/lib/"
  install -Dm644 README "$pkgdir/usr/share/$pkgname/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"  
}
