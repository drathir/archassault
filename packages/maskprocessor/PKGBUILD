# Maintainer: ArchAssault <team@archassault.org>
pkgname=maskprocessor
pkgver=0.71
pkgrel=1
groups=('archassault' 'archassault-cracker')
pkgdesc="A High-Performance word generator with a per-position configurable charset"
arch=('i686' 'x86_64')
url='http://hashcat.net/wiki/doku.php?id=maskprocessor'
license=('custom')
makedepends=('p7zip')
source=("http://hashcat.net/tools/maskprocessor/maskprocessor-$pkgver.7z")
sha512sums=('40d30f049ecac130512aac4c0cad72af86758fa4919ca6048ba23137bac258a415cfe182b91c837e4121207604cf6c7da6f2d879a463af49ba3c446e54371e8f')

prepare() {
  cd "$srcdir/maskprocessor-$pkgver"

  sed -i 's|dicts/|"$1"/|' generate.sh
  sed -i 's|./mp64.bin|/usr/bin/maskprocessor|' generate.sh
}

package() {
  cd "$srcdir/maskprocessor-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/maskprocessor"

  # Bin.
  if [[ "$CARCH" == 'x86_64' ]]; then
    install -m755 mp64.bin "$pkgdir/usr/bin/mp64"
    ln -s mp64 "$pkgdir/usr/bin/maskprocessor"
  elif [[ "$CARCH" == 'i686' ]]; then
    install -m755 mp32.bin "$pkgdir/usr/bin/mp32"
    ln -s mp32 "$pkgdir/usr/bin/maskprocessor"
  fi
  install -m755 generate.sh "$pkgdir/usr/share/maskprocessor"
}
