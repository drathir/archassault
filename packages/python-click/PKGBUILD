# Maintainer: ArchAssault <team@archassault.org>
# Contributor:  Carl George <carl@cgtx.us>

_module="click"
pkgname=("python-click" "python2-click")
pkgver=3.3
pkgrel=2
pkgdesc="A simple wrapper around optparse for powerful command line utilities."
url="http://github.com/mitsuhiko/click"
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
license=('BSD')
arch=('any')
source=("https://pypi.python.org/packages/source/c/click/click-${pkgver}.tar.gz"
	'LICENSE')
sha512sums=('fe2f85d5f60c26c9dbfff5d689e21e8f728d6f6b092ab93a66339b1cc7ee25d8e2b21235e07f5fbcf16ae1a2fb252537679019c5edc3ca9bf8ced57e39e9a67f'
            '1c98cceb8a193e9337a3f35cba7d1c0503a32f2efba66f2a11671520a05d6b38dcf7cc1ec2e359ecca50c7fc826a8d64bd086c682e2e58e8ac120ea462e5967e')

prepare() {
        cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
        cd "${srcdir}/${_module}-${pkgver}"
        python setup.py build
        cd "${srcdir}/${_module}-${pkgver}-python2"
        python2 setup.py build
}


package_python-click() {
        depends=("python>=3.3")
        cd "${srcdir}/${_module}-${pkgver}"
        python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
       install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/python-click/LICENSE
}

package_python2-click() {
        depends=("python2>=2.6")
        cd "${srcdir}/${_module}-${pkgver}-python2"
        python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
        install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/python2-click/LICENSE
}
