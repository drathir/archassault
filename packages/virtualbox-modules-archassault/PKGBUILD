# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sébastien Luttringer

pkgbase=virtualbox-modules-archassault
pkgname=('virtualbox-host-modules-archassault' 'virtualbox-guest-modules-archassault')
pkgver=4.3.20
pkgrel=2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-archassault' 'linux-archassault<3.18'
             'linux-archassault-headers' 'linux-archassault-headers<3.18'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-3.17-archassault

build() {
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules-archassault(){
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux-archassault' 'linux-archassault<3.18')
  replaces=('virtualbox-modules-archassault')
  conflicts=('virtualbox-modules-archassault')
  install=virtualbox-host-modules-archassault.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

package_virtualbox-guest-modules-archassault(){
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux-archassault' 'linux-archassault<3.18')
  replaces=('virtualbox-archlinux-modules-archassault')
  conflicts=('virtualbox-archlinux-modules-archassault')
  install=virtualbox-guest-modules-archassault.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:
md5sums=()
