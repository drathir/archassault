# Maintainer: ArchAssault <team archassault org>
# Contributor : Marcos Heredia <chelqo@gmail.com>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: orelien <aurelien.foret@wanadoo.fr>
# Contributor: Muflone <webreg@vbsimple.net>

pkgname=httrack
pkgver=3.48.19
pkgrel=1
pkgdesc="An easy-to-use offline browser utility."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archassault' 'archassault-webapps')
url="http://www.httrack.com/"
license=('GPL')
depends=('xdg-utils' 'hicolor-icon-theme')
options=('!libtool')
install="$pkgname.install"
source=("http://mirror.httrack.com/historical/$pkgname-$pkgver.tar.gz")
sha512sums=('c7202c4d86963fe935ce1f1a81f43f47544e03e4743528f52acd541ba87d6ef47d9e691b432f9bebedbf06e764bd67fe85748762b6ec3c09491b7528957cda8b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  _docdir=${pkgdir}/usr/share/doc/${pkgname}
  _licdir=${pkgdir}/usr/share/licenses/${pkgname}
  install -dpm755 ${_docdir} ${_licdir}
  install -Dpm644 AUTHORS INSTALL* README ${_docdir}/
  install -Dpm644 COPYING license.txt ${_licdir}/
}
