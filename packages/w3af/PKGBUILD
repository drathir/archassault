# Maintainer: ArchAssault <team@archassault.org>
pkgname=w3af
pkgver=1.6.0.5
pkgrel=2
groups=('archassault' 'archassault-webapp' 'archassault-fuzzer' 'archassault-scanners')
pkgdesc="Web Application Attack and Audit Framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://w3af.sourceforge.net/'
license=('GPL')
if [[ $CARCH == x86_64 ]]; then
     depends=('python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'python2-pdfminer' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3'
         'python2-xdot' 'libxml2' 'libxslt' 'lib32-glibc' 'halberd' 'python2-dartspylru-git')
else
     depends=('python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'python2-pdfminer' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3'
         'python2-xdot' 'libxml2' 'libxslt' 'halberd' 'python2-dartspylru-git')
fi
options=('!strip')
source=("git+https://github.com/andresriancho/w3af.git#tag=${pkgver}"
        'w3af.desktop' 'archassault-version.patch' 'w3af-no-update.patch')
sha512sums=('SKIP'
            '9cd4c950f503d5930c450c6cfe551d2e630fd594d44a15e58c466b3c3eeabfc2cf268ef5f3322a03c681d4a9192493832bd07c454da35425e5efb5018f7c0787'
            'eaf0a50f7ab801b734c3aae36e4bbf1ae1af6389d7f4a31684c0c6395bcaca677e163841cd98de0be0eafdb0c95367e6a4ae1a41d1963cad7188f2f9c4e54234'
            '1cf6d9322bb21a3ca04641eb80ed38a5acae640bda6b28975667351e7debfa939ff51563e66e0508a33c5f9ac41f0bebb9944fef81886ed4f7f2cbf9d5d2d710')

prepare() {
  cd "$srcdir/w3af"

  # Delete some of the stuff from w3af-extlib (stuff that needs to be installed) -> dependencies
  rm -rf w3af/extlib/fpconst-0.7.2
  rm -rf w3af/extlib/SOAPpy

  # Remove dependency checks.
  sed -i 's/dependency_check()/#&/' w3af_{console,gui}

  # Change the default metasploit location.
  sed -i 's|/opt/metasploit3/bin/|/usr/share/metasploit/|' w3af/core/controllers/misc_settings.py

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" $(find $srcdir -name '*.py') tools/* w3af_*

  sed -i -e "s|#![ ]*/usr/bin/python2.5|#!/usr/bin/env python2|" $(find $srcdir -name '*.py')

  patch -Np1 -i ../w3af-no-update.patch
  patch -Np1 -i ../archassault-version.patch
}

package() {
  cd "$srcdir/w3af"
  install -dm755 "$pkgdir/usr/share/w3af"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/applications/"
  cp --no-preserve=ownership -a * "$pkgdir/usr/share/w3af"
  install -Dm644 "$srcdir/w3af.desktop" "$pkgdir/usr/share/applications"
  # Joint script for w3af_console.
  cat > "$pkgdir/usr/bin/w3af" <<EOF
#!/bin/sh
python2 /usr/share/w3af/w3af_console "\$@"
EOF

  # Joint script for w3af_gui.
  cat > "$pkgdir/usr/bin/w3af-gui" <<EOF
#!/bin/sh
python2 /usr/share/w3af/w3af_gui "\$@"
EOF

  chmod +x "$pkgdir/usr/share/w3af"/w3af_{console,gui} "$pkgdir/usr/bin"/{w3af,w3af-gui}
}
