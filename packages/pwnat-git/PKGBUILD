# Maintainer: ArchAssault <team@archassault.org>

_pkgname=pwnat
pkgname=${_pkgname}-git
pkgver=20140907.r9.1d07c2e
pkgrel=1
groups=('archassault' 'archassault-networking')
pkgdesc="Punches holes in firewalls and NATs allowing any numbers of clients behind NATs to directly connect to a server behind a different NAT with no 3rd party, port forwarding, DMZ or spoofing involved"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://samy.pl/pwnat/'
license=('GPL3')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git+https://github.com/samyk/${_pkgname}.git")
sha512sums=('SKIP')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")

pkgver() {
    cd $pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $pkgname
    make
}

package() {
    cd $pkgname
    install -Dm755 $_pkgname "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 README "$pkgdir"/usr/share/${_pkgname}/README
    install -Dm644 README-udptunnel "$pkgdir"/usr/share/${_pkgname}/README-udptunnel
}
