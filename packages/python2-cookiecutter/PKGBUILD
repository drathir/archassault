# Maintainer: ArchAssault <team archassault org>
# Contributor: Meir Kriheli <mkriheli@gmail.com>

pkgname=python2-cookiecutter
_pkgname=cookiecutter
pkgver=0.8.0
pkgrel=1
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/audreyr/cookiecutter"
license=('BSD')
depends=('python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://github.com/audreyr/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('4d5091abb1201b4d6cd227c865f263c0f8d7946e1c0fedba71aafa982e9217732bf9f791e71fb3d68a52dd1c4562d300c3215be35f33d3c65c0332da84fdca60')

prepare(){
  grep -iRl 'python' "$srcdir/$_pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
      cd "$srcdir/${_pkgname}-${pkgver}"
      python2 setup.py install --root="$pkgdir/" --optimize=1
      install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
