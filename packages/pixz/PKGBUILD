# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=pixz
pkgver=1.0.2
pkgrel=1
pkgdesc="Parallel, indexed xz compressor"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archassault' 'archassualt-misc')
url="https://github.com/vasi/pixz"
license=('GPL')
depends=('libarchive')
makedepends=('asciidoc')
conflicts=('pixz-git')
source=(https://github.com/vasi/pixz/archive/v${pkgver}.tar.gz)
sha512sums=('718309849bd5981c3efb6df135ef2ba97f902ec03bf00d5b325d54b605d303c7238106077baafd1a053841e0b00ae75fceb9692fa1eb278e36b648c7a55232ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix missing -lm
  sed -i 's/-llzma/-lm -llzma/' Makefile
  make
  # generate manpage
  a2x --doctype manpage --format manpage pixz.1.asciidoc
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm 755 pixz   "${pkgdir}/usr/bin/pixz"
  install -Dm 644 pixz.1 "${pkgdir}/usr/share/man/man1/pixz.1"
}
