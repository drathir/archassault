# Maintainer: ArchAssault <team@archassault.org>
pkgname=sasm
pkgver=3.1.0
pkgrel=1
pkgdesc="A simple crossplatform IDE for NASM, MASM, GAS and FASM assembly languages"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/Dman95/SASM"
license=('custom')
if [[ $CARCH == 'x86_64' ]]; then
      depends=('nasm' 'libxcb' 'gdb' 'gcc-libs-multilib' 'qt5-base' 'mesa')
else
      depends=('nasm' 'libxcb' 'gdb' 'gcc-libs' 'qt5-base' 'mesa')
fi
makedepends=('qt4>=4.8')
sha512sums=('884375b07ebb51dd4cf0b1afa8e8cfe826e8a0cc48ed3f781081288a9af8e80031707913a235f013260c84e98f260811c2e048542aeb7f6f83dce2e79d876ede')
source=("https://github.com/Dman95/SASM/archive/v$pkgver.tar.gz")

build() {
  cd "$srcdir/SASM-$pkgver"
  qmake QMAKE_CXXFLAGS="$CFLAGS -Wa,--noexecstack -Wl,-z,noexecstack -W -D_REENTRANT -fPIE" QMAKE_LFLAGS="-Wl,-z,noexecstack"
  make CXXFLAGS="$CXXFLAGS -Wa,--noexecstack -W -D_REENTRANT -fPIE" LDFLAGS="-Wl,-z,noexecstack"
}

package() {
  cd "$srcdir/SASM-$pkgver"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/sasm"
  install -dm755 "$pkgdir/usr/share/doc/sasm"
  install -dm755 "$pkgdir/usr/share/sasm/NASM"
  install -dm755 "$pkgdir/usr/share/sasm/Projects"
  install -dm755 "$pkgdir/usr/share/sasm/include"
 
  # Bins
  install -Dm755 Linux/bin/fasm "$pkgdir/usr/bin/fasm"
  install -Dm755 Linux/bin/listing "$pkgdir/usr/bin/listing"
  install -Dm755 sasm "$pkgdir/usr/bin/sasm"
  
  # Docs & License
  install -Dm644 Linux/share/doc/sasm/* "$pkgdir/usr/share/doc/sasm/"
  install -Dm644 Linux/share/doc/sasm/copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"

  # Share dir
  install -Dm644 Linux/share/sasm/keys.ini "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/sasm.png "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/NASM/* "$pkgdir/usr/share/sasm/NASM/"
  install -Dm644 Linux/share/sasm/Projects/* "$pkgdir/usr/share/sasm/Projects/"
  install -Dm644 Linux/share/sasm/include/* "$pkgdir/usr/share/sasm/include/"
  
}

# vim:set ts=2 sw=2 et:
