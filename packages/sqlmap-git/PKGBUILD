# Maintainer: ArchAssault <team@archassault.org>
pkgname=sqlmap-git
pkgver=20141211.r6455
pkgrel=1
epoch=1
groups=('archassault' 'archassault-webapps' 'archassault-exploits' 'archassault-fuzzers')
pkgdesc="An automatic SQL injection tool developed in Python"
url='https://github.com/sqlmapproject/sqlmap'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('python2')
makedepends=('git')
options=(!strip)
source=("${pkgname}::git+https://github.com/sqlmapproject/sqlmap")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp --no-preserve=ownership -R * "${pkgdir}/usr/share/${pkgname}"

  if [[ ${CARCH} == i686 ]]; then
    rm "${pkgdir}/usr/share/${pkgname}/udf/mysql/linux/64/lib_mysqludf_sys.so"
    rm "${pkgdir}/usr/share/${pkgname}/udf/postgresql/linux/64/8.2/lib_postgresqludf_sys.so"
    rm "${pkgdir}/usr/share/${pkgname}/udf/postgresql/linux/64/8.3/lib_postgresqludf_sys.so"
    rm "${pkgdir}/usr/share/${pkgname}/udf/postgresql/linux/64/8.4/lib_postgresqludf_sys.so"
  fi

  cat > "${pkgdir}/usr/bin/sqlmap" << EOF
#!/bin/bash
cd /usr/share/sqlmap-git
python2 ./sqlmap.py "\$@"
EOF
  chmod +x "${pkgdir}/usr/bin/sqlmap"
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
   -e "s|#![ ]*/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/local/bin/python$|#!/usr/bin/python2|" \
   $(find ${pkgdir} -name '*.py')
}
