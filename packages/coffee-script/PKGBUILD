# Maintainer: ArchAssault <team@archassault.org>
_pkgname=coffeescript
pkgname=coffee-script
pkgver=1.8.0
pkgrel=1
pkgdesc='A little language that compiles into javascript'
arch=('any')
url='http://coffeescript.org'
license=('MIT')
depends=('nodejs')
optdepends=("nodejs-profile: to load ${pkgname} in node")
source=("https://github.com/jashkenas/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}.1")
sha512sums=('7868c65f4379e4bc905c0edfaf93c106da47c9f50d6f9a7097a7f0cec94b65835120cbe3bb85e08d3cf74d7bf238ce5d64ef182f338a4abd1f381ab1faf905f7'
            'c6a86759df5695cd26f57ff98bea74f17f0f2e8d06b53dbd035841f0bff82524b8b018d5527fb78efa6d1463dd4a68779ee634fddd016b8cc7a4babade593a2e')

prepare() {
    cd ${_pkgname}-${pkgver}
    npm install
}

check() {
    cd ${_pkgname}-${pkgver}
    bin/cake test
    bin/cake test:browser
}

build() {
    cd ${_pkgname}-${pkgver}
    bin/cake build
    bin/cake build:parser
    bin/cake build:browser
}

package() {
    install -Dm644 ${pkgname}.1 "${pkgdir}/usr/share/man/man1/coffee.1"
    cd ${_pkgname}-${pkgver}
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    # cake writes bullshit to ~/.noode-libraries, so set HOME to local dir
    HOME="$PWD" bin/cake --prefix "${pkgdir}/usr/" install
    install -d "${pkgdir}/usr/lib/node_modules"
    mv "${pkgdir}/usr/lib/${pkgname}" "${pkgdir}/usr/lib/node_modules/${pkgname}"
    ln -sf /usr/lib/node_modules/${pkgname}/bin/{cake,coffee} "${pkgdir}/usr/bin/"
    cd "${pkgdir}/usr/lib/node_modules/${pkgname}"
    npm install
}
