# Maintainer: ArchAssault <team@archassault.org>
pkgname=suricata
pkgver=2.0.5
pkgrel=1
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
groups=('archassault')
depends=('nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnet' 'libpcap' 'libyaml' 'python2-magic' 'file')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=(http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz)
sha512sums=('9e9a9b92aa966cad17c3a215ddb81ea512e3a5af45b884f2fae5a4c6c188868ccc2be1bac83e57e68be1f8000b3fb1bbd7a36d9c7a88c156ecdbfb3013107c56')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc HAVE_PYTHON_CONFIG=/usr/bin/python2
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
  install -d ${pkgdir}/var/log/$pkgname
  install -Dm644 $pkgname.yaml "${pkgdir}/etc/$pkgname/$pkgname.yaml"
  install -Dm644 classification.config "${pkgdir}/etc/$pkgname/classification.config"
  install -Dm644 reference.config "${pkgdir}/etc/$pkgname/reference.config"
  install -d "${pkgdir}/etc/$pkgname/rules"
  install -Dm644 rules/*.rules "${pkgdir}/etc/$pkgname/rules/"
}
