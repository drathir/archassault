# Maintainer: ArchAssault <team archassault.org>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>


pkgname=yasat
pkgver=755
pkgrel=2
pkgdesc="A simple stupid audit tool"
arch=('any')
license=('GPL3')
depends=('bash')
backup=('etc/yasat/yasat.conf')
groups=('archassault')
url="http://yasat.sourceforge.net/"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'yasat.conf')
sha512sums=('5a2a86c1f828f3fee5b84447ce6607b0dbdd8a16d8425dac910ef1c6f0d606105e851af35eedada46bf6438fb7cdde3f178803d8adf91e86f3bae4ce06151f44'
            '5e5403dad904128782216431c8c55a59fac02a8bb0b4df92649e1dc89e679a0648cbaaf593cee91fbdb60dbfcd62ecfa020d65894b4ad45d7e541b40cf854d11')

package() {
	cd $srcdir/$pkgname

	install -D -m0755 yasat "$pkgdir"/usr/bin/yasat

	install -d -m0755 "$pkgdir"/usr/share/yasat/

  # plugins
  cp -R plugins/ "$pkgdir"/usr/share/yasat/
  chmod 755 "$pkgdir"/usr/share/yasat/plugins
  chmod 644 "$pkgdir"/usr/share/yasat/plugins/*       # force 644 on everything, then...
  chmod 755 "$pkgdir"/usr/share/yasat/plugins/*.test  # add execute to *.test

  # common
	install -D -m0644 yasat.css "$pkgdir"/usr/share/yasat/
  install -m0644 common "$pkgdir"/usr/share/yasat/common
	install -m0644 osdetection "$pkgdir"/usr/share/yasat/osdetection

  # default/sample config file
  install -D -m0755 "$srcdir"/yasat.conf "$pkgdir"/etc/yasat/yasat.conf

  # man pages
	install -D -m0644 man/yasat.8 "$pkgdir"/usr/share/man/man8/yasat.8

  # the script attempts to build this file dynamically at run time.
  # this means this file needs loose permissions if you want to run
  # yasat as a non-root user.
  touch "$pkgdir"/usr/share/yasat/yasat.advices
  chmod 644 "$pkgdir"/usr/share/yasat/yasat.advices
}

