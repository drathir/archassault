# Maintainer: ArchAssault <team at archassault dot org>
pkgname=maltrieve-git
pkgver=r148.4ad4045
pkgrel=1
groups=('archassault' 'archassault-malware')
pkgdesc="A tool to retrieve malware directly from the source for security researchers."
arch=('any')
url='https://github.com/technoskald/maltrieve'
license=('GPL3')
depends=('python2' 'python2-beautifulsoup4' 'python2-lxml' 'python2-feedparser' 'python2-gevent' 'python2-greenlet' 'python2-grequests' 'python2-requests' 'python2-wsgiref')
makedepends=('git')
provides=('maltrieve')
source=("git+https://github.com/technoskald/maltrieve.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/maltrieve"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/maltrieve" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/maltrieve"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/maltrieve"
  install -dm755 "$pkgdir/usr/bin"

  #Bins
  install -Dm644 grequests.py "$pkgdir/usr/share/maltrieve/"
  install -Dm755 maltrieve.py "$pkgdir/usr/share/maltrieve/"
  install -Dm755 maltrievecategorizer.sh "$pkgdir/usr/share/maltrieve/"
  install -Dm644 maltrieve.cfg "$pkgdir/usr/share/maltrieve/" 
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
 
cat > "$pkgdir/usr/bin/maltrieve" << EOF
#!/bin/sh
cd /usr/share/maltrieve
python2 maltrieve.py "\$@"
EOF

cat > "$pkgdir/usr/bin/maltrievecategorizer" << EOF
#!/bin/sh
cd /usr/share/maltrieve
bash maltrievecategorizer.sh "\$@"
EOF

chmod +x "$pkgdir/usr/bin/maltrievecategorizer"
chmod +x "$pkgdir/usr/bin/maltrieve"

}
