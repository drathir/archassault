# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=nvidia-304xx-archassault
pkgver=304.123
_extramodules=extramodules-3.16-archassault
pkgrel=3
pkgdesc="NVIDIA drivers for linux-archassault, 304xx legacy branch"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-archassault>=3.16' 'nvidia-304xx-libgl' "nvidia-304xx-utils=${pkgver}")
makedepends=('linux-archassault-headers>=3.16')
conflicts=('nvidia-archassault' 'nvidia-96xx-archassault' 'nvidia-173xx-archassault')
provides=('nvidia-304xx')
license=('custom')
install=nvidia-304xx-archassault.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('52307054483f5c40391608ce69114d48')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('9d461e9e00e473f458aee96cd6d85101')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-304xx-archassault.conf"
    echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-304xx-archassault.conf"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 ${_pkg}/LICENSE "${pkgdir}/usr/share/licenses/nvidia-304xx-archassault/LICENSE"
}
