# Maintainer: ArchAssault <team@archassault.org>
pkgname=fakedns-git
pkgver=20140804.r17.87d4216
pkgrel=1
groups=('archassault' 'archassault-spoof' 'archassault-proxy')
pkgdesc="A regular-expression based python MITM DNS server with correct DNS request passthrough and \"Not Found\" responses."
arch=('any')
url="https://github.com/Crypt0s/FakeDns"
license=('GPL3')
depends=('python2')
makedepends=('git')
replaces=('fakedns')
source=("git+https://github.com/Crypt0s/FakeDns.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}"/FakeDns
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/FakeDns
  sed -i 's|bin/python$|bin/python2|' *.py
}

package() {
  cd "${srcdir}"/FakeDns
  install -dm755 "${pkgdir}"/usr/bin/
  install -Dm644 dns.conf.example "${pkgdir}"/usr/share/"${pkgname}"/dns.conf.example
  install -Dm755 fakedns.py "${pkgdir}"/usr/share/"${pkgname}"/fakedns.py
  install -Dm644 README.md "${pkgdir}"/usr/share/doc/"${pkgname}"/README.md
  ln -s "/usr/share/${pkgname}/fakedns.py" "${pkgdir}"/usr/bin/fakedns
}
